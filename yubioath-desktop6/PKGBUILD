# Maintainer: haxibami <contact@haxibami.net>
pkgname=yubioath-desktop6
_pkgname=yubioath-desktop
pkgver=6.0.2
pkgrel=1
pkgdesc="Crossplatform graphical user interface for YubiKey one-time passwords"
arch=('x86_64')
url="https://www.yubico.com/blog/introducing-yubico-authenticator-6-for-desktop/"
license=('Apache 2.0')
conflicts=("yubioath-desktop" "yubioath-desktop-beta-bin")
depends=('pcsclite' 'ccid' 'gnome-screenshot')
options=(!strip)
source=("https://developers.yubico.com/yubioath-flutter/Releases/yubico-authenticator-${pkgver}-linux.tar.gz")
sha256sums=("87e58d27d6f3767dfc47f05f88142e1f56938e62af92a6156a7d8b0be963c931")

package() {
    cd ./yubioath-desktop-release-linux
    install -dm755 "${pkgdir}/opt/${_pkgname}"
    install -dm755 "${pkgdir}/usr/bin"

    # binary
    install -Dm755 ./authenticator "${pkgdir}/opt/${_pkgname}/authenticator"

    # link executable
    ln -s "/opt/${_pkgname}/authenticator" "${pkgdir}/usr/bin/${_pkgname}"

    # application assets
    cp -r {data,helper,lib} "${pkgdir}/opt/${_pkgname}"

    # desktop entry & icon
    sed -i \
      -e "s|@EXEC_PATH/authenticator|${_pkgname}|" \
      -e "s|@EXEC_PATH/linux_support/com.yubico.yubioath.png|com.yubico.yubioath|" \
      ./linux_support/com.yubico.authenticator.desktop
    install -Dm644 ./linux_support/com.yubico.authenticator.desktop "${pkgdir}/usr/share/applications/com.yubico.yubioath.desktop"
    install -Dm644 ./linux_support/com.yubico.yubioath.png ${pkgdir}/usr/share/icons/hicolor/128x128/apps/com.yubico.yubioath.png

}
